<html>
  <head>
    <meta charset="UTF-8">
    <title>CPSC 314 Assignment 6 Sept 2023</title>
    <style>
      body { margin: 0; } canvas {width: 100%;
      height: 100% }
    </style>
  </head>
  <body>
    <div id="canvas"></div>
    <script src="js/three.js"></script>
    <script src="js/OBJLoader.js"></script>
    <script src="js/SourceLoader.js"></script>
    <script src="js/OrbitControls.js"></script>
    <script src="js/KeyboardState.js"></script> <!-- By Jerome Etienne: http://jetienne.com/ -->

<script id="raytraceVertShader" type="x-shader/x-vertex">

void main()
{       
  gl_Position = projectionMatrix * modelViewMatrix * vec4(position,1.0);
}

</script>

<script id="raytraceFragShader" type="x-shader/x-fragment">

uniform vec2 resolution;
uniform float myFloat1;
uniform float myFloat2;

// uncomment if using    https://shdr.bkcore.com/

// int maxBounces=2;        // if using  https://shdr.bkcore.com/
// vec3 lightPosition = vec3(-2,2.5,-6);

// uncomment if using   a6.html
uniform int maxBounces;
uniform vec3 lightPosition;



const int n_spheres = 4;
float epsilon = 0.00001;
float largeT = 1000000.0;
vec3 backgroundColor = vec3(0.8,0.83,1.0);

struct light {
  vec3 color;
  vec3 position;
};

struct ray {
  vec3 origin;
  vec3 direction;
  int bounces;
};

struct material {   // surface material parameters
  float kLocal;     
  float kReflect;
  vec3 color;
};

struct sphere {
  material mtrl;
  vec3 position;
  float radius;
  bool isLight;
};

struct plane {
  material mtrl;
  vec3 position;
  float width;
  float height;
};

sphere nearestSphere;
sphere scene_spheres[n_spheres];

/////////////////////////////////////////////////////////////////////////////////
// sphereIntersect():  returns smallest posistive t value for a sphere intersection; 
//                     returns -1 if no intersection 
/////////////////////////////////////////////////////////////////////////////////

float sphere_intersect(ray myRay, sphere sphr) {
  float a = dot(myRay.direction, myRay.direction);
  vec3 s0_r0 = myRay.origin - sphr.position;
  float b = 2.0 * dot(myRay.direction, s0_r0);
  float c = dot(s0_r0, s0_r0) - (sphr.radius * sphr.radius);
  float d = sqrt(b*b-4.0*a*c);               // compute the discriminant
  if (d < 0.0) {                 // no solution to the quadratic equation?
    return -1.0;                   // then flag as no intersection
  } else {
    float t1 = (-b - d)/(2.0*a);   // compute both values of t
    float t2 = (-b + d)/(2.0*a);   
    float tmin = min(t1,t2);
    float tmax = max(t1,t2);
    if (tmax<=0.0+epsilon) return -1.0;
    if (tmin<=0.0+epsilon) return -1.0;
    return (tmin);              // return smallest positive value
  }
}

/////////////////////////////////////////////////////////////////////////////////
// nearestT():  cast a ray, and computes t for closest intersection in the direction of +t
//          If there is no intersection, it returns  largeT
/////////////////////////////////////////////////////////////////////////////////

float nearestT(ray myRay)
{
  float nearest_t = largeT;
  float curr_t;
  for (int i = 0; i < n_spheres; ++i) {                   
    curr_t = sphere_intersect(myRay, scene_spheres[i]);  // test against sphere
    if (curr_t == -1.0) continue;         // missed sphere?
    else if (curr_t < nearest_t) {        // closest sphere?
      nearest_t = curr_t;
      nearestSphere = scene_spheres[i];
    }
  }
  return nearest_t;
}

/////////////////////////////////////////////////////////////////////////////////
// localShade():  compute local color for a surface point
/////////////////////////////////////////////////////////////////////////////////

vec3 localShade(vec3 P, vec3 N, vec3 surfColor) {
  ray shadowRay;

/// TO DO: (c) (d)
//  (1) compute and return simple diffuse shading, i.e., (N.L)*surfColor,
//      where surfColor is the assigned color of the surface. Clamp values as appropriate.
//  (2) later, additionally check to see if the object is in shadow by building and casting
//      a shadow ray.  If the point is in shadow, return black. Otherwise return the diffuse shading.

  vec3 L = normalize(lightPosition - P); 
  vec3 diffuseColor = dot(N, L) * surfColor;

  shadowRay.origin = P; 
  shadowRay.direction = lightPosition - P; 
  nearestT(shadowRay); 
  if (nearestSphere.isLight==false) {
    return 0.1 * surfColor; 
  }

  return diffuseColor; 
}

/////////////////////////////////////////////////////////////////////////////////
// bgColor(ray):  cast a ray, and compute a color, recursively if needed
/////////////////////////////////////////////////////////////////////////////////

vec3 bgColor(ray myRay) 
{
    float yPlane = -10.0;
  float t = (yPlane - myRay.origin.y)/myRay.direction.y;
  if (t<0.0) return backgroundColor;            // ray intersects behind the eye, so is looking away from the plane
    vec3 P = myRay.origin + t*myRay.direction;        // compute intersection point

  float shadowScale = 1.0;
  ray shadowRay;
  shadowRay.origin = P;
  shadowRay.direction = lightPosition - P;
  float tS = nearestT(shadowRay);
  if (nearestSphere.isLight==false) // occluding objects have been found
    shadowScale = 0.4; 

  float xf = fract(0.1*P.x)-0.5; 
  float zf = fract(0.1*P.z)-0.5; 
  float r = 0.27;
  float F1 = r - abs(xf);
  float F2 = r - abs(zf);
  if (F1>0.0 && F2>0.0)
    return shadowScale*vec3(0.6,0.3,0.3);      // black square
  else
    return shadowScale*vec3(1.0,1.0,1.0);            // white square
}

/////////////////////////////////////////////////////////////////////////////////
// rayCast2():  cast the reflected ray, and compute a color for it
/////////////////////////////////////////////////////////////////////////////////

vec3 rayCast2(ray myRay)             // return the color for this reflected ray
{
// TODO:  this will be a slightly simplified version of rayCast()
// (1) find the nearest intersection 
// (2) if hit an object, then compute and return the local color;
//     otherwise return black
  float nearest_t = nearestT(myRay);
  if (nearest_t < largeT) {
    // TODO
    vec3 P = myRay.origin + myRay.direction * nearest_t; 
    vec3 N = normalize(P - nearestSphere.position); 
    vec3 localColor = localShade(P, N, nearestSphere.mtrl.color); 
    return localColor;     
  }

  return bgColor(myRay);       // return checkeboard texture
}

/////////////////////////////////////////////////////////////////////////////////
// rayCast():  casts the initial ray and computes a color, recursively if needed
/////////////////////////////////////////////////////////////////////////////////

vec3 rayCast(ray myRay)             // return color
{
// TODO:
// (1) if hit an object, i.e., a sphere, then compute and return the local color;
//     otherwise returns the background color, via bgColor(myRay)

  float nearest_t = nearestT(myRay);              // returns t val of closest object; nearestSphere is also set
  float kReflect = nearestSphere.mtrl.kReflect;         // keep these lines for use in step (7)
  float kLocal = nearestSphere.mtrl.kLocal;       // keep these lines for use in step (7)
  if (nearest_t < largeT) {       // hit an object?
    // (b)
    // return nearestSphere.mtrl.color;           // color this white;  replace this with steps 1 above; and later with the steps below)

    // (c)
    // (2) compute the actual intersection point, P, given the nearest_t value;
    // (3) compute the normal, N;  the center of the sphere is given by   nearestSphere.position
    // (4) call the localShade function to compute the local shading
    // (4.5)  you will notice that the light source sphere itself ends up being black.
    //        This is because the light source point is inside the sphere, so N.L = -1 everywhere.
    //        You can test for this and then compute local shading with a flipped normal
    //        for this case. I.e., if nearestSphere.isLight==true  then
    //        call localShade(), but with -N instead of N.
    vec3 P = myRay.origin + myRay.direction * nearest_t; 
    vec3 N = normalize(P - nearestSphere.position); 
    if (nearestSphere.isLight) return nearestSphere.mtrl.color;  
    vec3 localColor = localShade(P, N, nearestSphere.mtrl.color); 

// (5) develop the parameters for the reflected ray

   ray reflectedRay;
   reflectedRay.origin = P; 
   reflectedRay.direction = reflect(myRay.direction, N); 

// (6) compute the color of the reflected ray; 
//     Normally this would be a recursive call to rayCast(), but GLSH does not support recursion,
//     so we will instead call rayCast2():
   vec3 reflectedColor = rayCast2(reflectedRay);

// (7) return the sum of the local color and the reflected ray, weighted by kLocal and kReflect
   return (kLocal*localColor + kReflect*reflectedColor);
  }
  return bgColor(myRay);   // if no objects are hit, then return the background checkerboard or sky color 
}
/////////////////////////////////////////////////////////////////////////////////
// initialize():  define the scene:  a set of spheres
/////////////////////////////////////////////////////////////////////////////////

void initialize()
{
  // INIT SPHERES

  sphere sphere0;
    sphere0.mtrl.color = vec3(1.0, 1.0, 0.0);
  sphere0.mtrl.kReflect = 0.0;
  sphere0.mtrl.kLocal = 1.0;
  sphere0.radius = 0.1;
  sphere0.position = lightPosition;
  sphere0.isLight = true;

  sphere sphere1;
  sphere1.mtrl.color = vec3(0.5, 1, 0.5);
  sphere1.mtrl.kReflect = 0.6;
  sphere1.mtrl.kLocal = 0.5;
  sphere1.radius = 3.0;
  sphere1.position = vec3(-2.5,-2,-16);
  sphere1.isLight = false;

  sphere sphere2;
  sphere2.mtrl.color = vec3(0.0, 0.0, 1.0);
  sphere2.mtrl.kReflect = 0.0;
  sphere2.mtrl.kLocal = 1.0;
  sphere2.radius = 1.0;
  sphere2.position = vec3(2,1,-15);
  sphere2.isLight = false;

  sphere sphere3;
  sphere3.mtrl.color = vec3(1.0, 0.5, 1.0);
  sphere3.mtrl.kReflect = 0.2;
  sphere3.mtrl.kLocal = 0.8;
  sphere3.radius = 2.5;
  sphere3.position = vec3(1,0.5,-13.0);
  sphere3.position.x = 1.0 + 2.0*myFloat1;   // animated sphere
  sphere3.position.y = -3.0 + 2.0*myFloat2;   // animated sphere
  sphere3.isLight = false;

  scene_spheres[0] = sphere0;
  scene_spheres[1] = sphere1;
  scene_spheres[2] = sphere2;
  scene_spheres[3] = sphere3;  
}

/////////////////////////////////////////////////////////////////////////////////
// main()
/////////////////////////////////////////////////////////////////////////////////

void main() {
  initialize();   // define the world geometry, i.e., four spheres

    // compute normalized screen coordinates for pixel:
    // v lies in [-1,1] in y direction;   v=1 is top-of-screen;  v=-1 is bottom-of-screen
    // u lies in [-a,a] in x direction, a=aspect ratio;  u=-a is left-of-screen;  u=a is right-of-screen
  float a = resolution.x/resolution.y;
  vec2 uv = (-1.0 + 2.0*gl_FragCoord.xy / resolution.xy) * vec2(a, 1.0);   // normalized screen coords (u,v)

   // define the ray (in VCS) that corresponds to the current fragment
   // P(0,0,0) --> P(x,y,-1.0)    point on the plane z=-1.0, where x=uv[0], y=uv[1]
  ray pixelRay;
  float sf = 0.5;    // scale factor that controls field-of-view
  pixelRay.origin = vec3(0,0,0);                       // ray starts at eye:  origin
  pixelRay.direction = normalize(vec3(sf*uv,-1.0));    // look in the direction of a given pixel

  vec3 rayColor = rayCast(pixelRay);            // cast the initial ray
  gl_FragColor = vec4(rayColor, 1.0);           // assign color to fragment
}
</script>

<script src="a6.js"></script>
</body>
</html>
